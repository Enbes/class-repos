(* A1: intro2.ml *)

(* This assignment is subject to revision until its post date. 
   It will be graded on the following scale (0-4pts): 
  
     4pts: Perfect; you did all the exercises (or maybe all but one) 
           and got pretty much everything right.

     3pts: You made a good effort; got most exercises done but perhaps 
           not all; got most exercises correct.

     2pts: You did fewer than half the exercises. 

     1pt:  You only did a few of the exercises. 

     0pt:  You didn't do any of the exercises.      

   In certain cases, we might assign half points (e.g., 3.5 for an
   effort somewhere between 3 and 4).
 *)

(** EXERCISE 1: Fill out the following README: *)

(* README

 Name: Corbin Dotson
 Year (e.g., Sophomore, Junior): Junior
 ID: cd725014@ohio.edu 

 *)

(** EXERCISE 2: Sign up for Piazza. 

    Make sure you've signed up for Piazza (you were supposed to do so in 
    the previous assignment :-)).
*)

(** IGNORE ME -- CODE FOR WRITING TEST CASES *)
let check
    (string_of_a:'a -> string)
    (string_of_b:'b -> string)
    (name:string)
    (f:'a -> 'b)
    (x:'a)
    (expected:'b) =
  if f x = expected then
    ()
  else
    Printf.printf "%s %s = %s, expected %s\n"
      name (string_of_a x) (string_of_b (f x)) (string_of_b expected)
;;

let rec string_of_list (string_of_a:'a -> string) (l: 'a list) =
    match l with
    | [] -> "[]"
    | x :: l' -> string_of_a x ^ " :: " ^ string_of_list string_of_a l'
;;

let string_of_intList = string_of_list string_of_int;;

let parenthesize (f:'a -> string) (x:'a) : string = "(" ^ f x ^ ")";;
(** END IGNORE ME *)

(** ************ PART I: More OCaml Intro. **************)

(** (MUTUAL) RECURSION *)

(* As a warmup, consider the following function, [evenb], that returns a
   Boolean value [true] if its integer argument [x] is even and [false]
   otherwise (in case [x] is odd). *)

(* precondition: x >= 0 *)
let rec evenb (x : int) : bool =
  if x = 0 || x = 2 then true
  else if x = 1 then false
  else evenb (x - 2)

(*
# evenb 23;;
- : bool = false
# evenb 24;;
- : bool = true
 *)

(* This function introduces a couple new OCaml features that we may have 
   only briefly looked at in class: 
   - The type [bool], of Boolean values
   - The equality comparison operator (=), as in [x = 0]
   - The Boolean disjunction operator (||), as in [x = 0 || x = 2]
   - The [let rec] keyword, for introducing recursive function 
     definitions (we definitely saw this one in lecture). *)

(* In OCaml, it's possible to define mutually recursive functions as
   well. For example, consider the following pair of functions [f] and 
   [g] that mutually recursively decrement from an integer [x >= 0] down 
   to 0: *)

(* precondition: x >= 0 *)
let rec f (x : int) : int =
  if x = 0 then 0 else g (x-1)

and g (x : int) : int =
  if x = 0 then 0 else f (x-1)

(*
# f 3;;
f 3;;
- : int = 0
# g 3;;
g 3;;
- : int = 0
 *)

(* In general, a group of mutually recursive functions is defined by 
   introducing the first function with [let rec ...] and the remaining 
   functions in the group with [and ...]. *)

(** EXERCISE 3 *)

(* Using only function definitions, if/then/else, mutual recursion,
   and subtraction by 1, define a pair of mutually recursive functions 
     - even : int -> bool 
     - odd : int -> bool 
   s.t. [even n = true] iff [n] for [n >= 0] is even and 
        [odd n = true] iff [n] for [n >= 0] is odd. 
   In particular, your definitions of [even] and [odd] should not refer
   to [evenb] defined above (or the 'mod' function).

   Examples: 

     even 3 = false
     even 27 = false
     even 40 = true
     odd 3 = true
     odd 0 = false
*)

let rec even (x:int) : bool = 
				if x = 0 then true
				else odd (x-1)
and odd (x:int) : bool = 
			   if x = 0 then false
			   else even (x-1)
;;

let check_even = check string_of_int string_of_bool "even" even;;
let check_odd = check string_of_int string_of_bool "odd" odd;;

Printf.printf "EXERCISE 3\n";;

check_even 3 false;;
check_even 27 false;;
check_even 40 true;;
check_even 0 true;;
check_even 1 false;;
check_even 44 true;;

check_odd 0 false;;
check_odd 1 true;;
check_odd 3 true;;
check_odd 41 true;;

(** DATA TYPES *)
			 
(* The basic data types in OCaml include everything you'd typically see in 
   imperative languages like C. We've already seen 
     - int       Fixed-width signed integers
     - bool      The Boolean values [true] and [false]
   but there's also
     - float     Floating-point numbers
     - char      Characters
     - string    Sequences of characters
   (See http://caml.inria.fr/pub/docs/manual-ocaml/coreexamples.html for 
    more details.) *)

(* In addition, OCaml supports a number of compound or composite types
   (as well as user-defined types, which we'll see later on in this 
   assignment).  For example, the type of pairs of integer values is
   written: *)

type intPair = (int * int)       (* The type of pairs of integers *)

(* [type intPair ...] introduces a type "synonym", a new name for the 
   the type [(int * int)], the type of pairs of integers. *)

(* Pairs, or values of pair type, are written 
   [(<fst-val>, <snd-val>)]: *)

let intPair_example : intPair = (1, 2)

(* To extract the first or second element of a pair, use 
   [fst] for the first component and [snd] for the second. *)

let x = fst intPair_example (* = 1 *)

let y = snd intPair_example (* = 2 *)

(* It's also possible (and many times preferable) to use let-syntax 
   to destruct pairs. For example: *)

let (z, w) = intPair_example;;
z + w;; (* = 3 *)

(* The types on the two sides of a pair need not be the same. For example, 
   here's a type synonym for the type of integer-Boolean pairs: *)

type intBoolPair = (int * bool)

let intBoolPair_example : intBoolPair = (3, false)

(** EXERCISE 4 *)

(* Using [fst], [snd], and the pair constructor [( , )],
   define a function, [swapFstSnd], that takes a pair of integers and 
   swaps their components. So for example, [swapFstSnd (1, 2) = (2, 1)]. *)

let swapFstSnd (p:(int*int)) : int*int =
  let (x, y) = p in (y, x)
;;

let check_swapFstSnd =
  let string_of_intPair (x,y) = "(" ^ string_of_int x ^ "," ^ string_of_int y ^ ")" in 
  check string_of_intPair string_of_intPair
    "swapFstSnd" swapFstSnd
;;

Printf.printf "EXERCISE 4\n";;

check_swapFstSnd (1,2) (2,1);;
check_swapFstSnd (0,0) (0,0);;
check_swapFstSnd (1,7) (7,1);;

(** ************ PART II: Lists **************)
					  
(* The second composite data type we'll look at in this assignment is
   lists, which are an abstraction of the linked lists you'd typically
   code yourself in a language like C.

   In OCaml, a list is either empty, written [], or a "cons" of a "head"
   element, say 1, tacked on to the front of a "tail" list, say l. 

     NOTE: Previously in this document, square brackets "[ ... ]" were 
     used to delimit code examples within comments. Since "[ ... ]" plays
     double-duty to denote lists, we'll use quotation marks instead of 
     square brackets in this section to delimit code.

   Cons is written as "1 :: l". For example: the list containing the 
   integers 1, 2, 3 in that order can be written in OCaml as: *)

let list1 : int list = 1 :: 2 :: 3 :: []

(* That is, list1 is 
     1 cons'd onto 
       (the list 2 cons'd onto 
          (the list 3 cons'd onto 
             the empty list [])) *)
					
(* Lists can also be built with the syntax: *)

let list1' = [1; 2; 3]

(* which is equivalent to the above, and in general preferable because 
   it's more compact. *)

(* The type of list1 (and of list1') is "int list", which stands for
   "list of integers". In general, lists may range over any type (they 
   are what is called "polymorphic"), so we can have "bool list", "(int
   list) list" (the type of lists of lists of integers), etc. *)

(* As a complement to building lists, we'd also like to have a way to 
   compute with them. This is done by what's called "pattern-matching"
   in OCaml. Example: *)

let rec sumIntList (l : int list) : int =
  match l with
  | [] -> 0
  | x :: l' -> x + sumIntList l'

(*
# sumIntList list1;;
sumIntList list1;;
- : int = 6
# sumIntList list1';;
sumIntList list1';;
- : int = 6
*)

(* Here's a breakdown: 

let rec sumIntList (l : int list) : int =
  match l with      <-- Start a "pattern match" on the list l. 
                        That is, we'll consider all the ways in which we 
                        could have built the list l (hint: there are only 
                        two). For each case, we may associate variables with 
                        the parts of the list we're matching, and will define a 
                        different result -- probably depending on the variables 
                        we matched to various parts of the list -- in each 
                        case. 

  | [] -> 0         <-- So for example, when l is the empty list [], we just return 0.

  | x :: l' ->      <-- Otherwise, when l is composed of head x cons'd onto tail l',
    x +             <-- return x added to 
    sumIntList l'   <-- sumIntList recursively applied to the tail l'. 

                        In the above, the variable "x" in the pattern " x :: l' " 
                        is bound to the head (first element) of the list l, and may 
                        be referred to in the body of the match. Likewise, l' is 
                        bound to the tail of the list l (all elements except the first
                        one) and may also be referred to in the body of the match. *)

(** EXERCISE 5 *)			      
   
(* Using pattern-matching, recursion, and a let-bound inner auxiliary function,
   define a new function, sumFirstLast (l : int list) : int, that sums 
   the first and last elements of an integer list. If the list l is empty,
   sumFirstLast should return 0. If l contains just a single element, then 
   that element is both first and last. For example:

     sumFirstLast [1; 2; 3] = 4
     sumFirstLast [1] = 2
     sumFirstLast [] = 0
     sumFirstLast [-1; 1] = 0
   
*)
			      
let sumFirstLast (l:int list) : int = 
  match l with
  | [] -> 0
  | x :: r ->
     let rec getTail (l':int list) : int =
       match l' with
       | [] -> 0
       | x' :: r' ->
	  let t = getTail r' in
	  if t = 0 then x' else t
     in
     x + getTail l
;;

let check_sumFirstLast =
  check (parenthesize string_of_intList) string_of_int 
    "sumFirstLast" sumFirstLast
;;

Printf.printf "EXERCISE 5\n";;

check_sumFirstLast [1;2;3] 4;;
check_sumFirstLast [1] 2;;
check_sumFirstLast [] 0;;
check_sumFirstLast [-1; 1] 0;;
check_sumFirstLast [1;2;3;4;5;6;7;8;9;1] 2;;
			      
(** EXERCISE 6 *)  

(* Define a recursive function, decToBin (n : int) : bool list, that returns the
   little-endian binary encoding, as a list of boolean values, of the
   positive integer n. For example,
   
     decToBin 1 = [true]
     decToBin 2 = [false; true]
     decToBin 3 = [true; true]
     decToBin 23 = [true; true; true; false; true]
 *)

(* precondition: n > 0 *)
let rec decToBin (n : int) : bool list =
  if n > 0
  then let t = n mod 2 in
       if t = 0
       then false :: decToBin (n / 2)
       else true :: decToBin (n / 2)
  else []
;;

let check_decToBin =
  check string_of_int (parenthesize (string_of_list string_of_bool))
    "decToBin" decToBin
;;

Printf.printf "EXERCISE 6\n";;

check_decToBin 1 [true];;
check_decToBin 2 [false; true];;
check_decToBin 3 [true; true];;
check_decToBin 23 [true; true; true; false; true];;

(** ************ PART III: Data Types **************)

(* If you recall from class, it's possible in OCaml for users both to define 
   their own data types and to use standard OCaml language features, like 
   pattern-matching, to compute over values of user-defined types. 

   For example, here's the type of 2d points:
 *)

type point = (float * float);;	       

(* The type [point] is really just an abbreviation for the type of pairs 
   of floats, written (float * float). *)

(* Of course, more interesting data types are encodable as well. For example, 
   the following is a (highly inefficient) unary encoding of the natural 
   numbers, defined as an OCaml data type: *)

type nat =
  | Zero           (* a constructor for the natural number 0 *)
  | Succ of nat    (* "successor": a constructor for n + 1 *)
;;

(* The constructor [Zero] represents the natural number 0. [Succ n] represents, 
   in unary, the natural number n + 1. So the encoding of 3, for example, is 
   Succ (Succ (Succ Zero), or 1 1 1 0. 

   Here's a highly inefficient function to add two unary natural numbers (it's 
   running time is linear in the value of n): *)  

let rec addNat (n : nat) (m : nat) : nat =
  match n with
  | Zero -> m (* Zero plus any m is just m *)
  | Succ n' -> Succ (addNat n' m)	      

(* We can do better, of course. One way is just to use good old machine 
   integers, type [int] in OCaml. But such integers have a fixed range, as 
   determined by the word size of our machine. 

   Here's a representation of the positive natural numbers (starting
   from one) that gives arbitrary precision, just like [nat] above,
   yet doesn't pay quite as steep a performance penalty: *)

type binNat =
  | One
  | BO of binNat
  | BI of binNat   
;;

(* THIS FUNCTION USED FOR TESTING BELOW *)
let rec string_of_binNat (b:binNat) : string =
  match b with
  | One -> "One"
  | BO b' -> "(BO " ^ string_of_binNat b' ^ ")"
  | BI b' -> "(BI " ^ string_of_binNat b' ^ ")"
;;

(* END *)

(* Do you see what's going on here? We're using a binary representation instead 
   of a unary one, in which: 
     - [One] represents the number 1
     - [BO n] represents, for a given [binNat] [n], the number [2 * n], and 
     - [BI n] represents [(2 * n) + 1]. 
   For example, here's the encoding of 5: *)

let two = BO One;;
let five = BI two;;
five;;  

  (* Another way to think about these constructors: One is the bit string 1, BO n 
     shifts n left by 1 and adds 0, BI n shifts n left by 1 and adds 1. *)
  
(** EXERCISE 7 *)  
  
(* Write a function, intToBinNat (x : int) : binNat, that maps an integer [x] to 
   the equivalent [binNat]. You may assume that [x > 0]. *)

(* precondition: x > 0 *)
let rec intToBinNat (x : int) : binNat =
  if x > 1
  then
    if even x
    then BO (intToBinNat (x / 2))
    else BI (intToBinNat ((x - 1) / 2))
  else One
;;

let check_intToBinNat =
  check string_of_int string_of_binNat
    "intToBinNat" intToBinNat
;;

Printf.printf "EXERCISE 7\n";;

check_intToBinNat 1 One;;
check_intToBinNat 2 two;;
check_intToBinNat 5 five;;
check_intToBinNat 10 (BO five);;
check_intToBinNat 6 (BO (BI One));;
check_intToBinNat 11 (BI five);;

(** EXERCISE 8 *)  
  
(* Write a function, binNatToInt (x : binNat) : int, that maps a binary positive
   natural [x] to the equivalent integer. *)

let rec binNatToInt (x : binNat) : int =
  match x with
  | One -> 1
  | BO n -> 2 * binNatToInt n
  | BI n -> 2 * binNatToInt n + 1
;;

let check_binNatToInt =
  check string_of_binNat string_of_int
    "binNatToInt" binNatToInt
;;

Printf.printf "EXERCISE 8\n";;

check_binNatToInt One 1;;
check_binNatToInt two 2;;
check_binNatToInt five 5;;
check_binNatToInt (BO five) 10;;
check_binNatToInt (BO (BI One)) 6;;
check_binNatToInt (BI five) 11;;

(* To convince yourself that your [intToBinNat] and [binNatToInt] functions are 
   defined correctly, write at least three test cases that exploit the fact that 
   [intToBinNat] and [binNatToInt] are inverses. That is, it should be the case that:
    
      For all positive integers n, binNatToInt (intToBinNat n) = n
 *)

(** ************ PART IV: Polymorphism **************)

(* In class, we defined a so-called "polymorphic" list data type that looked like: *)

type 'a mylist =
  | Nil
  | Cons of ('a * 'a mylist)

(* Recall that ['a] is a type variable, sort of like "<T>" in a generic class in  
   Java or C++. The type ['a list] can be instantiated to any old type we like. For 
   example, the type [int mylist] giving the type of lists with integer elements. 

   (We call this new datatype [mylist] so as not to clash with OCaml's
    standard [list].)
 *)

(* As we also saw in class, it's possible to write generic functions
   over polymorphic data types, like the following generic map
   function: *)

let rec mymap (f : 'a -> 'b) (l : 'a mylist) : 'b mylist =
  match l with
  | Nil -> Nil
  | Cons(x, l') -> Cons (f x, mymap f l')
;;
  
mymap (fun x -> x * x) (Cons(1, Cons(2, Cons(3, Nil))));;
mymap (fun x -> not x) (Cons(false, Cons(true, Nil)));;

(** EXERCISE 9 *)

(* Write your own polymorphic data type, for binary trees. Your type 
   declaration should begin: 
 
     type 'a mytree = ... 

   and should contain two constructors, one for leaves (which contain 
   no data) and one for nodes which contain a value of type 'a, a left 
   subtree and a right subtree. 

   Name the leaf constructor "Leaf" and the node constructor "Node".

   Encode the arguments to the Node constructor in the following order:
     - first the data element, of type 'a
     - then the left subtree
     - then the right subtree.

   For example, a valid [int mytree] might be:
  
     Node(3, Node(4, Leaf, Leaf), Node(5, Leaf, Leaf))
*)  

type 'a mytree =
  | Leaf
  | Node of ('a * 'a mytree * 'a mytree)
;;

(** EXERCISE 10 *)

(* Write a function, sumMyTree, that sums the integer elements in an 
   [int mytree] (that is, a [mytree] specialized to ['a = int]). Write 
   at least 5 test cases to convince yourself that your function works. *)

let rec sumMyTree (t : int mytree) : int = 
  match t with
  | Leaf -> 0
  | Node (n, l, r) -> n + sumMyTree l + sumMyTree r
;;

if sumMyTree Leaf = 0
then Printf.printf ""
else Printf.printf "E10 T1 fail\n"
;;
  
if sumMyTree (Node (1, Leaf, Leaf)) = 1
then Printf.printf ""
else Printf.printf "E10 T2 fail\n"
;;
  
if sumMyTree (Node (1, Node (1, Leaf, Leaf), Leaf)) = 2
then Printf.printf ""
else Printf.printf "E10 T3 fail\n"
;;
  
if sumMyTree (Node (1, Leaf, Node (1, Leaf, Leaf))) = 2
then Printf.printf ""
else Printf.printf "E10 T4 fail\n"
;;
  
if sumMyTree (Node (1, Node (1, Leaf, Leaf), Node (1, Leaf, Leaf))) = 3
then Printf.printf ""
else Printf.printf "E10 T5 fail\n"
;;

(** EXERCISE 11 *)

(* Using your [mytree] definition as a starting point, write a generic 
   function [mytreeMap] that maps a function of type ['a -> 'b] over 
   the elements of an ['a mytree]. Write at least 5 test cases (perhaps 
   using your trees from the test cases of EXERCISE 10) to convince yourself
   that your implementation works. *)

(* let rec mytreeMap (f : 'a -> 'b) (t : 'a mytree) : 'b mytree = ... *)
  
  
